$ = +World
	scene: +Group
		origin: +Xyz(0., 0., 0.)
		parts: +List using '<<'
			# Generic lighting and shader for parts using a single light source
			# with ambient lighting
			+Light
				origin: +Xyz(-3.5, 5.5, 10.0)
				ambient: +Color(0.5882, 0.5882, 0.5882, 1.0)
				lightColor: +Color(1.0, 1.0, 1.0, 1.0)
			# Skybox
			Shape.NewSphere(499.0, 20)
				origin: +Xyz(0., 0., 0.)
				diffuse: +Color(0.5882, 0.5882, 0.5882, 1.0)
				tex: +Texture
					image: @tycho_cyl_glow.png
				shader: @shaders/diffuse_texshader
			@fighter
				origin: +Xyz(0.0, 1., 6.)
				orientation: +Quat
			@sun
				origin: +Xyz(0.0, 0., 0.0)
				orientation: +Quat
			@flamingo
				origin: +Xyz(4., 2., 6.)
				orientation: +Quat
	camera: +Camera
		origin: +Xyz(0., 2., 5.)
		yaw: 0.
		spinrate: 0.
		velocity: 0.
		shader: @shaders/diffuse_shader
		
	render: +List using '<<'
		$.camera

	input: +Object
		keyDown: +Object
			key_up: []
				$.camera.velocity = 4.
			key_down: []
				$.camera.velocity = -4.
			key_left: []
				$.camera.spinrate = 4.
			key_right: []
				$.camera.spinrate = -4.
			key_kp_8: [] {@fighter .pitching = -2.}
			key_kp_6: [] {@fighter .yawing = -2.}
			key_kp_4: [] {@fighter .yawing = 2.}
			key_kp_2: [] {@fighter .pitching = 2.}
			key_V: [] 
				$.camera
					.origin.Set(0., 0., 0.)
					.orientation.Set(1., 0., 0., 0.)
					.following = @fighter
		keyUp: +Object
			key_up: []
				$.camera.velocity = 0.
			key_down: []
				$.camera.velocity = 0.
			key_left: []
				$.camera.spinrate = 0.
			key_right: []
				$.camera.spinrate = 0.
			key_kp_2: [] {@fighter .pitching = 0.}
			key_kp_4: [] {@fighter .yawing = 0.}
			key_kp_6: [] {@fighter .yawing = 0.}
			key_kp_8: [] {@fighter .pitching = 0.}
	animate: [dt]
		@sun .animate(dt)
		@flamingo .animate(dt)
		@fighter .animate(dt)
		$.camera
			.yaw = (.yaw + dt*.spinrate).Modulus(2.*Float.pi) if .spinrate!=0.
			.origin.Add(-.velocity * dt * .yaw.Sin, 0., -.velocity * dt * .yaw.Cos)
			.orientation.SetAngleAxis(.yaw, 0., 1., 0.)
